type state_enl = ON | SEND | OFF
node enlace(c, status: bool) returns (estado:state_enl)
let
    automaton

        state STOFF do
            estado = OFF;
        unless status then STON

        state STON do
            estado = ON;
        unless not status then STOFF
            | c then STSEND

        state STSEND do
            estado = SEND;
        unless not status then STOFF
            | not c then STON

    end
tel

type prot = HTTP | SMTP | FTP
node protocolos(nota_http, nota_smtp, nota_ftp:int) returns (estado: prot; melhor_valor:int)
let
    automaton

        state STHTTP do
            estado = HTTP;
            melhor_valor = nota_http;
        unless nota_ftp <= nota_http then STFTP
            | nota_smtp <= nota_http then STSMTP

        state STFTP do
            estado = FTP;
            melhor_valor = nota_ftp;
        unless nota_http <= nota_ftp then STHTTP
            | nota_smtp <= nota_ftp then STSMTP

        state STSMTP do
            estado = SMTP;
            melhor_valor = nota_smtp;
        unless nota_ftp <= nota_smtp then STFTP
            | nota_http <= nota_smtp then STHTTP

    end
tel



node controlador_multihomed (c3g, status_3g, ceth, status_eth, cwf, status_wf: bool; nota_http3g, nota_smtp3g, nota_ftp3g, nota_httpwf, nota_smtpwf, nota_ftpwf, nota_httpeth, nota_smtpeth, nota_ftpeth:int) returns (estado_3g, estado_eth, estado_wf: state_enl; prot_3g, prot_wf, prot_eth:prot; valor_3g, valor_wf, valor_eth: int)
    (* contract
        var 
            regra3g: bool;
    let
        regra3g = not ((valor_3g <= valor_wf) & (valor_3g < valor_eth)) or (estado_3g = SEND);
    tel
    enforce regra3g
            with (c3g: bool) *)

let
    estado_3g = inlined enlace(c3g, status_3g);
    estado_eth = inlined enlace(ceth, status_eth);
    estado_wf = inlined enlace(cwf, status_wf);
    (prot_3g, valor_3g) = inlined protocolos(nota_http3g, nota_smtp3g, nota_ftp3g);
    (prot_wf, valor_wf) = inlined protocolos(nota_httpwf, nota_smtpwf, nota_ftpwf);
    (prot_eth, valor_eth) = inlined protocolos(nota_httpeth, nota_smtpeth, nota_ftpeth);
tel
