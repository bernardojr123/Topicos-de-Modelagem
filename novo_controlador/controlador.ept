type state_enl = ON | SEND | OFF
node enlace(c, status: bool) returns (estado:int)
let
    automaton

        state STOFF do
            estado = 0;
        unless status & c then STSEND
            | status then STON

        state STON do
            estado = 1;
        unless c then STSEND
            | not status then STOFF

        state STSEND do
            estado = 2;
        unless c then STON
            | not status then STOFF

    end
tel

type prot = HTTP | SMTP | FTP
node protocolos(nota_http, nota_smtp, nota_ftp:int) returns (estado: prot;melhor_valor:int)
let
    automaton

        state STHTTP do
            estado = HTTP;
            melhor_valor = nota_http;
        unless (nota_ftp <= nota_http & nota_ftp <= nota_smtp)then STFTP
            | nota_smtp <= nota_http then STSMTP

        state STFTP do
            estado = FTP;
            melhor_valor = nota_ftp;
        unless (nota_http <= nota_ftp & nota_http <= nota_smtp) then STHTTP
            | nota_smtp <= nota_ftp then STSMTP

        state STSMTP do
            estado = SMTP;
            melhor_valor = nota_smtp;
        unless (nota_http <= nota_smtp & nota_http <= nota_ftp) then STHTTP
            | nota_ftp <= nota_smtp then STFTP

    end
tel

node controlador_multihomed (status_3g, status_eth, status_wf: bool; nota_http3g, nota_smtp3g, nota_ftp3g, nota_httpwf, nota_smtpwf, nota_ftpwf, nota_httpeth, nota_smtpeth, nota_ftpeth:int) returns (estado_3g, estado_eth, estado_wf: int; prot_3g, prot_wf, prot_eth:prot; valor_3g, valor_wf, valor_eth: int;)
    contract
        var 
            regra3g, regraeth, regrawf: bool;
        let
            regraeth = not ((valor_eth <= valor_3g) & (valor_eth <= valor_wf) & estado_eth > 0) or (estado_eth = 2);
            regrawf = not ((valor_wf <= valor_3g) & (valor_wf < valor_eth) & estado_wf > 0) or (estado_wf = 2);
            regra3g = not ((valor_3g < valor_eth) & (valor_3g < valor_wf) & estado_3g > 0) or (estado_3g = 2);
        tel
        enforce regra3g & regraeth & regrawf 
                with (c3g, ceth, cwf: bool)


let
    estado_3g = inlined enlace(c3g, status_3g);
    estado_eth = inlined enlace(ceth, status_eth);
    estado_wf = inlined enlace(cwf, status_wf);
    (prot_3g, valor_3g) = inlined protocolos(nota_http3g, nota_smtp3g, nota_ftp3g);
    (prot_wf, valor_wf) = inlined protocolos(nota_httpwf, nota_smtpwf, nota_ftpwf);
    (prot_eth, valor_eth) = inlined protocolos(nota_httpeth, nota_smtpeth, nota_ftpeth);
tel
